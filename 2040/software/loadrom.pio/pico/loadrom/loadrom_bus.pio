; PIO bus engine for MSX PicoVerse loadrom
; RD SM drives data bus and WAIT/BUSSDIR
; WR SM captures address/data writes

.program msx_rd
.side_set 2 opt
.wrap_target
start:
    nop         side 0b11    ; WAIT=1 BUSSDIR=1
    mov osr, null            ; data bus input
    out pindirs, 8     side 0b11
wait_sltsl:
    wait 0 pin 27     side 0b11 ; PIN_SLTSL
wait_rd:
    wait 0 pin 24     side 0b11 ; PIN_RD
    nop         side 0b10    ; WAIT=0 BUSSDIR=1 (stretch read)
    nop         side 0b10    ; allow address to settle
    in pins, 16       side 0b10
    push block        side 0b10
    pull block        side 0b10
    mov x, osr               ; preserve data byte
    mov osr, ~null           ; data bus output
    out pindirs, 8     side 0b10
    mov osr, x               ; restore data byte
    out pins, 8        side 0b10
    nop         side 0b10    ; hold data with WAIT low
    nop         side 0b10    ; hold data with WAIT low
    nop         side 0b10    ; hold data with WAIT low
    nop         side 0b11    ; WAIT=1 BUSSDIR=1 (release read)
    wait 1 pin 24     side 0b11 ; PIN_RD
    mov osr, null            ; data bus input
    out pindirs, 8     side 0b11
    nop         side 0b11    ; WAIT=1 BUSSDIR=1
    jmp pin wait_sltsl       ; If SLTSL high, wait for next slot select
    jmp wait_rd              ; Otherwise keep serving back-to-back reads
.wrap

.program msx_wr
.wrap_target
wait_sltsl_wr:
    wait 0 pin 27            ; PIN_SLTSL
wait_wr:
    wait 0 pin 25            ; PIN_WR
    in pins, 32
    push block
    wait 1 pin 25            ; PIN_WR
    wait 1 pin 27            ; PIN_SLTSL
    jmp wait_sltsl_wr
.wrap
